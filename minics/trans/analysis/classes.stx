module analysis/classes

imports
  signatures/minics-sig
  analysis/types
  analysis/statements
  analysis/declarations

rules usingOk : scope * UsingDecl
  usingsOk maps usingOk(*, list(*))

  usingOk(s, Using(ref)).   // TODO

  usingOk(_, UsingDecl-Plhdr()).


rules classOk : scope * ClassDecl
  classesOk maps classOk(*, list(*))

  classOk(globalScope, class@Class(name, parent, members)) :- {classScope instanceScope methodScope CT IT}
    // Static class
    new classScope,
    classScope -P-> globalScope,
    CT == CLASS(name, classScope),
    globalScope -> Class{name@name} with type CT,

    // Instance object
    new instanceScope,
    instanceScope -P-> globalScope,
    IT == OBJECT(CT, instanceScope),

    declareVarOccurrence(instanceScope, Var{"this"@name}, IT),      // @name
    declareConstructor(classScope, IT),         // @name
    parentOk(classScope, instanceScope, parent),
    membersOk(classScope, instanceScope, members),
    @class.type := CT.

  classOk(_, ClassDecl-Plhdr()).

rules parentOk : scope * scope * ParentDecl

  parentOk(classScope, instanceScope, parent@NoParent()) :-
    // TODO: Inherit from Object.
    true.

  parentOk(classScope, instanceScope, parent@Parent(ref)) :- {parentClassScope parentInstanceScope CT PIT}
    typeOfClass(classScope, ref) == CT@CLASS(_, parentClassScope),
    // Ensure the ancestors are different from the class itself
    ensureNoInheritanceCycle(classScope, parentClassScope),
    classScope -E-> parentClassScope,

    // Get the parent's instanceScope.
    resolveConstructor(parentClassScope) == [(_, (_, PIT@OBJECT(_, parentInstanceScope)))],
    declareVarOccurrence(instanceScope, Var{"base"@ref}, PIT),     // @ref
    instanceScope -E-> parentInstanceScope,

    instanceScope -S-> classScope,

    @parent.type := CT.

  parentOk(_, _, ParentDecl-Plhdr()).

rules memberOk : scope * scope * MemberDecl
  membersOk maps memberOk(*, *, list(*))

  memberOk(classScope, instanceScope, field@InstanceField(type, name)) :- {T}
    typeOfType(classScope, type) == T,
    declareInstanceMember(instanceScope, classScope, name, T),
    @field.type := T.

  memberOk(classScope, _, field@StaticField(type, name)) :- {T}
    typeOfType(classScope, type) == T,
    declareStaticMember(classScope, name, T),
    @field.type := T.

  memberOk(classScope, instanceScope, method@InstanceMethod(returnType, name, params, stmts)) :- {methodScope RT PTS T}
    new methodScope,
    methodScope -P-> instanceScope,
    typeOfType(classScope, returnType) == RT,
    typesOfParams(methodScope, params) == PTS,
    T == METHOD(PTS, RT),
    declareInstanceMember(instanceScope, classScope, name, T),
    declareReturnType(methodScope, RT),      // @name
    stmtsOk(methodScope, stmts),
    @method.type := T.

  memberOk(_, _, MemberDecl-Plhdr()).

rules typeOfParam : scope * ParamDecl -> TYPE
  typesOfParams maps typeOfParam(*, list(*)) = list(*)

  typeOfParam(s, Param(type, name)) = T :-
    typeOfType(s, type) == T,
    declareVar(s, name, T),
    @name.type := T.

  typeOfParam(_, ParamDecl-Plhdr()) = _.
