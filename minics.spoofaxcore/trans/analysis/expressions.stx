module analysis/expressions

imports
  signatures/minics-sig
  analysis/types
  analysis/declarations

rules typeOfExp : scope * Exp -> TYPE

  typesOfExps maps typeOfExp(*, list(*)) = list(*)

  typeOfExp(s, e@Ref2Exp(ref)) = T :- {decl}
    typeOfRef(s, ref) == T,
    @e.type := T.

  typeOfExp(s, e@Call(e1, args)) = TR :- {T1 TPARAMS TARGS}
    typeOfExp(s, e1) == T1,
    T1 == METHOD(TPARAMS, TR) | error $[expected method, got [T1]],
    typesOfExps(s, args) == TARGS,
    TARGS == TPARAMS | error $[expected parameters of types [TPARAMS], got [TARGS]],
    @e.type := TR.

  typeOfExp(s, e@New(type)) = T :- {classScope CT}
    typeOfType(s, type) == CT,
    CT == CLASS(_, classScope)
      | error $[Expected class type, got [CT]],
    typeOfConstructor(classScope) == T,
    @e.type := T.

  typeOfExp(s, e@Member(e1, ref)) = T :- {T1 scope decl}
    typeOfExp(s, e1) == T1,
    getScope(T1) == scope,
    typeOfMember(scope, ref) == T,
    @e.type := T.

  typeOfExp(_, e@True()) = T :-
    T == BOOL(),
    @e.type := T.

  typeOfExp(_, e@False()) = T :-
    T == BOOL(),
    @e.type := T.

  typeOfExp(s, e@Lt(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[expected int, got [T1]],
    T2 == INT() | error $[expected int, got [T2]],
    @e.type := T.

  typeOfExp(s, e@Le(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[expected int, got [T1]],
    T2 == INT() | error $[expected int, got [T2]],
    @e.type := T.

  typeOfExp(s, e@Gt(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[expected int, got [T1]],
    T2 == INT() | error $[expected int, got [T2]],
    @e.type := T.

  typeOfExp(s, e@Ge(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[expected int, got [T1]],
    T2 == INT() | error $[expected int, got [T2]],
    @e.type := T.

  typeOfExp(s, e@Eq(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == T2 | error $[expected [T1], got [T2]],
    @e.type := T.

  typeOfExp(s, e@Neq(e1, e2)) = T :- {T1 T2}
    T == BOOL(),
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    @e.type := T.

  typeOfExp(s, e@And(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == BOOL() | error $[Expected [BOOL()], got [T1]],
    T2 == BOOL() | error $[Expected [BOOL()], got [T2]],
    T == BOOL(),
    @e.type := T.

  typeOfExp(s, e@Or(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == BOOL() | error $[Expected [BOOL()], got [T1]],
    T2 == BOOL() | error $[Expected [BOOL()], got [T2]],
    T == BOOL(),
    @e.type := T.

  typeOfExp(s, e@Not(e1)) = T :- {T1}
    typeOfExp(s, e1) == T1,
    T1 == BOOL() | error $[expected [BOOL()], got [T1]],
    T == BOOL(),
    @e.type := T.

  typeOfExp(s, e@Int(_)) = T :-
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Plus(e1)) = T :- {T1}
    typeOfExp(s, e1) == T1,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Min(e1)) = T :- {T1}
    typeOfExp(s, e1) == T,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Mul(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T2 == INT() | error $[Expected [INT()], got [T2]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Div(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T2 == INT() | error $[Expected [INT()], got [T2]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Add(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T2 == INT() | error $[Expected [INT()], got [T2]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Sub(e1, e2)) = T :- {T1 T2}
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    T1 == INT() | error $[Expected [INT()], got [T1]],
    T2 == INT() | error $[Expected [INT()], got [T2]],
    T == INT(),
    @e.type := T.

  typeOfExp(s, e@Cond(c, e1, e2)) = T :- {TC T1 T2}
    typeOfExp(s, c) == TC,
    typeOfExp(s, e1) == T1,
    typeOfExp(s, e2) == T2,
    TC == BOOL() | error $[Expected [BOOL()], got [TC]],
    lub(T1, T2) == T,
    @e.type := T.

  typeOfExp(_, Exp-Plhdr()) = _.

rules typeOfRef : scope * Ref -> TYPE

  typesOfRefs maps typeOfRef(*, list(*)) = list(*)

  typeOfRef(s, e@Var(ref)) = T :- {decl}
    typeOfVarMemberClass(s, ref) == T,
    @e.type := T.

  typeOfRef(s, e@This()) = T :-
    typeOfVar(s, "this") == T,
    @e.type := T.

  typeOfRef(s, e@Base()) = T :-
    typeOfVar(s, "base") == T,
    @e.type := T.

  typeOfRef(_, Ref-Plhdr()) = _.

rules getScope : TYPE -> scope

  getScope(OBJECT(_, instanceScope)) = instanceScope.
  getScope(CLASS(_, classScope)) = classScope.
