module MemberSemantics

language minics

start symbol Start

fixture [[
    using System;

    class A
    {
        public int fa;
        public bool MA() { }
    }

    class B : A
    {
        public bool fb;
        public int MB() { }
    }

    class C : B
    {
        public int fc;
        public int MC() { }
        [[...]]
    }
]]

test field [[
    public int x;
]] analysis succeeds
   run get-type to INT()

test field not unique [[
    public bool fc;
]] analysis fails

test field present in parent [[
    public int fb;
]] analysis fails

test method [[
    public int M() {}
]] analysis succeeds
   run get-type to METHOD([], INT())

test method not unique [[
    public bool MC() {}
]] analysis fails

test method present in parent [[
    public void MB() {}
]] analysis fails

test duplicate method with different signature (not supported in Mini C#) [[
    public int MC(int x) {}
]] analysis fails

test method with different signature present in parent (not supported in Mini C#) [[
    public void MB(int x) {}
]] analysis fails
